# Data utilities module for ClodMUD
# v0.0.0
# String representation of core objects and values

object 0
parent 1
name sys

method loaded
  args module
  vars module_interface

  module_interface = module

  module.exports.data = $data
  module.exports.to_string = (value) -> $data.to_string value


object 1
name root

default_parent $root

method spawn
  disallow overrides
  using create

  newObj = create @
  newObj.init()
  newObj

method root_name
  using toint
  vars name

  name ? '#' + toint(@)

method init
  disallow overrides
  using list_methods

  for p in @ancestors().reverse()
    for methodName in list_methods(p)
      if methodName.startsWith('init_')
        method = p[methodName]
        if method?.definer is p
          @[methodName]()

method ancestors
  using parent

  p = parent()
  if p? and p._id?
    [p, p.ancestors()...]
  else
    []


object $data

method to_string
  args value

  switch typeof value
    when 'boolean', 'number', 'string', 'undefined', 'bigint', 'symbol'
      value.toString()
    when 'object'
      @object_to_string value
    when 'function'
      @function_to_string value
    else
      "didn't recognize type of #{value}"

method object_to_string
  args obj

  return 'null' unless obj?

  if obj._id?
    @core_obj_to_string obj
  else if obj.definer?._id?
    @core_fn_to_string obj
  else
    JSON.stringify obj

method core_obj_to_string
  args obj

  if name = obj.root_name?()
    "$" + name
  else
    "#" + obj._id

method core_fn_to_string
  args fn

  definerStr = if fn.definer?._id?
    @core_obj_to_string fn.definer
  else
    "<unknown object>"

  "#{definerStr}.#{fn.name}"

method function_to_string
  args fn

  src = fn.toString()
  matched = src.match ///
    ^ function \s?
         (?<name> [^(]* )
      \( (?<args> [^)]* ) \)
  ///
  if matched
    {name, args} = matched.groups
    "#{name}(#{args}) { ... }"
  else
    src
