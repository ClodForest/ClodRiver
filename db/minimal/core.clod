# Minimal ClodMUD core v0.0.0
# Straight from the top of the dome of Robert de Forest <robert@defore.st>

object 0
parent 1
name sys

method startup
  using require, $module_loader
  args options = {}
  vars port, addr

  process = require 'node:process'
  {port = 7777, addr = 'localhost'} = options

  # Load modules
  session  = $module_loader.load 'session'
  net      = $module_loader.load 'networking'
  player   = $module_loader.load 'player'
  world    = $module_loader.load 'world'
  readline = $module_loader.load 'readline'

  # Get session exports
  sess = session.exports()

  # Configure session with readline, shutdown, and module loader
  sess.set_readline readline
  sess.set_shutdown => @shutdown()
  sess.set_module_loader $module_loader

  # Bootstrap world
  sess.bootstrap_world world

  # Subscribe to connection events
  net.on 'connected', (conn) ->
    sess.on_connect conn, player

  net.on 'line', (conn, line) ->
    sess.on_line conn, line

  net.on 'data', (conn, data) ->
    sess.on_data conn, data

  net.on 'disconnected', (conn) ->
    sess.on_disconnect conn

  # Start listener with module's connection class
  @spawn_listener net.exports().connection, {port, addr}

method shutdown
  using require

  (require 'node:process').exit()

method create
  using create, $root, add_obj_name
  args parent = $root, name

  newObj = create parent

  if typeof name is 'string' and name isnt ''
    add_obj_name name, newObj
    newObj.root_name(name)

  newObj

method add_obj_name
  using add_obj_name
  args name, obj

  add_obj_name name, obj

method del_obj_name
  using del_obj_name
  args name

  del_obj_name name

method add_methods
  using add_method
  args target, namesAndMethods

  for name, method of namesAndMethods
    add_method target, name, method

method textdump
  using textdump
  args relativePath

  textdump relativePath

method spawn_listener
  using listen
  args connection_class, options
  vars port, addr

  if options
    {port = 7777, addr = 'localhost'} = options

  newListener = connection_class.spawn()
  listen newListener, {port, addr}

method load_core
  using sender, load_core
  args relativePath, child_holder

  if sender() isnt $child_core_manager
    throw new Error "$sys.load_core is $child_core_manager-only"

  load_core relativePath, child_holder


object 1
name root

default_parent $root

method ancestors
  using parent

  p = parent()
  if p? and p._id?
    [p, p.ancestors()...]
  else
    []
  

method eval_on
  using $sys, compile
  args code, definer = @

  fn = compile code
  fn.definer = definer

  prefix = "eval_temp_"
  fnNumber = 0
  fnNumber++ while 'function' is typeof @[name = prefix + fnNumber]
  fn.name = name

  try
    $sys.add_methods(@, {[name]: fn})
    result = @[name]()

  finally
    $sys.rm_method(@, fn)

  return result


method children
  using children

  return children()


method descendents
  using children

  children().concat (children().map (child) -> child.descendents())...

method lookup_method
  disallow overrides
  using lookup_method
  args methodName, starting_ancestor = @

  lookup_method methodName, starting_ancestor

method init
  disallow overrides
  using toint, sender

  # Allow calls from self or from ancestors (for spawn pattern)
  if sender() isnt @ and sender() not in @ancestors()
    throw "perm"

  errors = {}

  for p in @ancestors().reverse()
    try
      methodName = "init_#{p.root_name()}"

      if (found = p[methodName]) and found.definer is p
        @[methodName]()

    catch e
      errors[toint p] = e

  errors

method spawn
  disallow overrides
  using $sys

  newObj = $sys.create(@)
  newObj.init()
  newObj

method root_name
  using $sys, toint
  args newName
  vars name

  if newName
    try
      $sys.del_obj_name(name)

    $sys.add_obj_name(newName, @)
    name = newName
  else
    if name
      return name

    '#' + toint(@)

method list_methods
  using list_methods

  list_methods()


object $child_core_manager

method load_core
  using sender, $sys, $module_loader
  args relativePath

  if sender() not in [$sys, $module_loader]
    throw new Error "Only $sys and $module_loader can load child cores."

  child = $child_core.spawn()
  $sys.load_core relativePath, child
  child


object $module_loader

method load
  using $child_core_manager, file_exists
  args module_name
  vars modules

  modules ?= {}

  path = "db/modules/#{module_name}.clod"

  unless file_exists null, path
    indexPath = "db/modules/#{module_name}/index.clod"
    if file_exists null, indexPath
      path = indexPath

  holder = $child_core_manager.load_core path

  modules[module_name] = holder
  holder

method get
  vars modules
  args module_name

  modules?[module_name]

method list_loaded
  vars modules

  modules ?= {}
  Object.keys modules

method list_available
  using list_dir, file_exists
  args subpath = '.'

  basePath = if subpath is '.' then 'db/modules' else "db/modules/#{subpath}"
  results  = []

  entries = list_dir null, basePath

  for entry in entries
    if entry.type is 'file' and entry.name.endsWith '.clod'
      results.push entry.name.replace /\.clod$/, ''
    else if entry.type is 'dir'
      indexPath = "#{basePath}/#{entry.name}/index.clod"
      if file_exists null, indexPath
        results.push entry.name + '/'

  results


object $child_core

method init_child_core
  disallow overrides
  vars listeners, exports

  listeners = {}
  exports   = {}

method on
  vars listeners
  args event_name, callback

  listeners[event_name] ?= []
  listeners[event_name].push callback

method _emit
  vars listeners
  args event_name, args...

  for callback in (listeners[event_name] ? [])
    callback args...

method accept_core
  using core_toobj, core_call
  vars child_sys, exports

  child_sys = core_toobj 0
  handler   = @

  # Create shared exports object stored in holder's vars
  exports = {}

  module = Object.assign {},
    request_methods: (args...) -> handler.request_methods args...
    emit:            (event, args...) -> handler._emit event, args...
    exports:         exports

  core_call child_sys, 'loaded', module

method exports
  vars exports

  exports

method request_methods
  vars child_sys
  args requests

  if sender isnt child_sys
    throw new Error ".request_methods can only be called by $sys in the child core"

  @check_obj_names Object.keys requests
  @copy_methods                requests

method copy_methods
  using toobj, core_toobj, sender

  if sender() isnt this
    throw new Error ".copy_methods is child-core-only"

  for objName, patterns of requests
    parentObj = toobj '$' + objName
    childObj  = core_toobj @, '$' + objName

    continue unless parentObj? and childObj?

    for methodName in list_methods parentObj
      for pattern in patterns
        if pattern instanceof RegExp
          matches = pattern.test methodName
        else
          matches = methodName is pattern

        if matches
          method = parentObj[methodName]
          add_method childObj, methodName, method.fn, method.source, {disallowOverrides: method.disallowOverrides}
          break


method check_obj_names
  args names

  unknowns = []

  for corename in unknowns
    if not obj = toobj "$" + corename
      unknowns.push corename

  if unknowns.length
    throw new Error "The following object names are not recognized in the parent core: " + unknowns.join ", "

  return


