# Test framework for ClodMUD
# v0.0.0

object $testing
parent $root

default_parent $testing


object $test

method run
  results = []

  for method_name in @list_methods()
    if method_name.startsWith 'test_'
      try
        result = @[method_name]()
        results.push [method_name, 'pass', result]
      catch e
        results.push [method_name, 'fail', e.toString()]

  results

method assert_true
  args value, message = "expected true"

  unless value
    throw new Error message

method assert_false
  args value, message = "expected false"

  if value
    throw new Error message

method assert_eq
  args actual, expected, message

  unless actual is expected
    throw new Error(message ? "expected #{expected}, got #{actual}")

method assert_throws
  args fn, pattern

  try
    fn()
    throw new Error "expected exception matching #{pattern}"
  catch e
    unless pattern.test(e.toString())
      throw new Error "expected exception matching #{pattern}, got: #{e}"


object $test_runner

method run_all
  using $test
  args tests = $test.descendents()

  results = {}

  for test in tests
    name = test.root_name()
    results[name] = test.run()

  results

method summarize
  args results

  total  = 0
  passed = 0
  failed = 0

  for name, test_results of results
    for [method_name, status, _] in test_results
      total++
      if status is 'pass'
        passed++
      else
        failed++

  {total, passed, failed}


# Example test
object $test_testing
parent $test

method test_assert_true_passes
  @assert_true true

method test_assert_eq_passes
  @assert_eq 1 + 1, 2

