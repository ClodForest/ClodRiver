# Ownership extensions for ClodMUD
# v0.0.0
# Simple ownership model
# - $owned:           something that can be owned
# - $ownership_group: a collection of owners (recursive membership)

object $ownership
parent $root

default_parent $ownership

method module_info
  version:  'v0.0.0'
  name:     'ownership'
  contents: $ownership.descendents()


object $ownership_group

method init_ownership_group
  vars members

  members ?= []

method is_member
  using $ownership_group
  vars members
  args candidate

  for member in members
    if member is candidate
      return true

    if $ownership_group.is_member_of(member) and member.is_member(candidate)
      return true

  false

method add_member
  using sender
  vars members
  args new_member

  unless sender() in members
    throw "permissions"

  members = [members..., new_member] unless new_member in members

method remove_member
  using sender
  vars members
  args old_member

  unless sender() in members
    throw "permissions"

  members = members.filter (m) -> m isnt old_member


object $owned

method init_owned
  vars owners

  owners ?= []

method owned_by
  disallow overrides
  using $ownership_group
  vars owners
  args prospective_owner

  for current_owner in owners
    return true if current_owner is prospective_owner
    return true if $ownership_group.is_member_of(current_owner) and current_owner.is_member(prospective_owner)

  false

method add_owner
  using sender
  vars owners
  args new_owner

  unless @owned_by(sender())
    throw "permissions"

  owners = [owners..., new_owner] unless new_owner in owners

method remove_owner
  using sender
  vars owners
  args old_owner

  unless @owned_by(sender())
    throw "permissions"

  owners = owners.filter (o) -> o isnt old_owner
