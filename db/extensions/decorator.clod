# Decorator pattern extensions for ClodMUD
# v0.0.0

###

The point of this module is to provide a way of making it so that all objects
in the text adventure world can have all traits without having to inherit
them. Placing a decorator on an object allows for customizing its traits in
that scope. When the decorator is absent, objects have sane defaults.

See the 'vr' module for examples.

###

# $decorators: management class for $decorator and $decorated
object $decorators
parent $root

method init_decorators
  vars decorators

  decorators ?= {}

method has_decorator
  args target, decorator

  decorator in target.decorations()


# $decorated: something which can be extended by adding decoration objects
object $decorated
parent $decorators

method init_decorated
  disallow overrides
  using sender, caller, $root
  vars decorations, details

  if sender() is @ and caller() is $root
    decorations = []
    details     = []
  else
    throw 'permissions'

method decorations
  vars decorations

  [decorations...]  # cloned to prevent mutation

method add_decoration
  vars decorations, details
  args decoration, detail

  unless decoration in decorations
    decorations = [decorations..., decoration]
    details     = [details...,     detail    ]

method rm_decoration
  vars decorations, details
  args decoration

  idx = decorations.indexOf decoration
  return if idx is -1

  decorations = decorations.filter (_, i) -> i isnt idx
  details     = details.filter     (_, i) -> i isnt idx

method get_details
  vars decorations, details
  args decorator

  details.filter (detail, idx) ->
    decorations[idx] is decorator


# $decoration: a trait that can be applied to $decorated objects
object $decoration
parent $root

method init_decoration
  using sender, caller, $root
  vars exports, defaults

  if sender() isnt @ or caller() isnt $root
    throw "permissions"

  exports  ?= {}
  defaults ?= {}

method set_exported
  vars exports
  args method, isExported = true

  if method.definer isnt @
    throw "invalid request"

  if isExported
    exports[method.name] = true
  else
    delete exports[method.name]

method use_decoration
  vars exports
  args decorated, methodName

  if exports[methodName]
    @[methodName](decorated)
