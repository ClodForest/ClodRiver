#!/usr/bin/env coffee

path     = require 'path'
fs       = require 'fs'
Core     = require '../lib/core'
TextDump = require '../lib/text-dump'

rootDir = path.resolve __dirname, '..'

clodFiles = process.argv[2..]

if clodFiles.length is 0
  clodFiles = [
    path.join rootDir, 'db', 'minimal', 'core.clod'
    path.join rootDir, 'db', 'extensions', 'test.clod'
  ]
else
  clodFiles = clodFiles.map (f) ->
    if path.isAbsolute f then f else path.resolve rootDir, f

core = new Core()

try
  for clodFile in clodFiles
    source   = fs.readFileSync clodFile, 'utf8'
    filename = path.basename clodFile
    dump     = TextDump.fromString source, filename
    dump.apply core

catch error
  console.error "Failed to load:", error.stack ? error
  process.exit 1

$test_runner = core.toobj '$test_runner'

unless $test_runner
  console.error "No $test_runner found. Did you load test.clod?"
  process.exit 1

try
  results = core.call $test_runner, 'run_all', []
  summary = core.call $test_runner, 'summarize', [results]

  for name, testResults of results
    for [methodName, status, detail] in testResults
      symbol = if status is 'pass' then '✓' else '✗'
      console.log "  #{symbol} #{name}.#{methodName}"
      if status isnt 'pass'
        console.log "      #{detail}"

  console.log ""
  console.log "#{summary.total} tests, #{summary.passed} passed, #{summary.failed} failed"

  if summary.failed > 0
    process.exit 1
  else
    console.log "Tests passing!"
    process.exit 0

catch error
  console.error "Test execution failed:", error.stack ? error
  process.exit 1
