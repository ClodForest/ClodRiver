#!/usr/bin/env coffee

{spawn} = require 'node:child_process'
path    = require 'node:path'
fs      = require 'node:fs'

rootDir = path.resolve __dirname, '..'
varDir  = path.join rootDir, 'var'
runDir  = path.join varDir, 'run'
logDir  = path.join varDir, 'log'
pidFile = path.join runDir, 'clodriver.pid'
logFile = path.join logDir, 'clodriver.log'

fs.mkdirSync runDir, {recursive: true} unless fs.existsSync runDir
fs.mkdirSync logDir, {recursive: true} unless fs.existsSync logDir

if fs.existsSync pidFile
  pid = parseInt fs.readFileSync(pidFile, 'utf8').trim()

  try
    process.kill pid, 0
    console.log "ClodRiver already running (PID #{pid})"
    process.exit 1
  catch
    console.log "Removing stale PID file"
    fs.unlinkSync pidFile

serverPath = path.join rootDir, 'bin', 'server.coffee'

logFd = fs.openSync logFile, 'a'

clodFiles = process.argv[2..]

nodeArgs = [
  '--enable-source-maps'
  '-r', path.join(rootDir, 'workaround', 'fix-coffee-line-numbers.js')
  serverPath
  clodFiles...
]

serverProc = spawn 'node', nodeArgs, {
  detached: true
  stdio:    ['ignore', logFd, logFd]
  cwd:      rootDir
}

fs.closeSync logFd

fs.writeFileSync pidFile, "#{serverProc.pid}\n"

serverProc.unref()

console.log "ClodRiver started (PID #{serverProc.pid})"
console.log "Logs: #{logFile}"
